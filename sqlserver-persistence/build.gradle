buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath "com.avast.gradle:gradle-docker-compose-plugin:${revDockerCompose}"
    }
}

apply plugin: 'docker-compose'

dependencies {
    compile project(':conductor-core')
    compile "com.google.inject:guice:${revGuice}"

    compile "com.microsoft.sqlserver:mssql-jdbc:${revMicrosoftSqlServerDriver}"
    compile "commons-io:commons-io:${revCommonsIo}"
    compile "com.zaxxer:HikariCP:${revHikariCP}"
    compile "org.flywaydb:flyway-core:${revFlywayCore}"

    testCompile project(':conductor-core').sourceSets.test.output
    testCompile "org.slf4j:slf4j-log4j12:${revSlf4jlog4j}"
}

dockerCompose{
    integrationTestDocker{
        useComposeFiles = ["./docker-compose.yml"]
        startedServices = ["sqlserver"]
        waitForTcpPorts = true
        isRequiredBy(tasks.test)
        projectName = project.name
    }
}

task intDockerStart() {
    group = "SqlServer data access based Integration Test"
    description "Starts a local db container for SqlServer data access integration tests."
    dependsOn integrationTestDockerComposeDownForced
    dependsOn integrationTestDockerComposeUp
}

task intDockerDown(dependsOn: integrationTestDockerComposeDownForced) {
    group = "SqlServer data access based Integration Test"
    description "Stops a local db container for SqlServer data access integration tests."
}

test.doFirst {
    dockerCompose.exposeAsSystemProperties(test)
}

test.finalizedBy("intDockerDown")

test {
    //the MySQL unit tests must run within the same JVM to share the same embedded DB
    maxParallelForks = 1
}
