plugins {
    id 'com.github.johnrengelman.plugin-shadow' version '2.0.3'
}

configurations {
    // Prevent shaded dependencies from being published, while keeping them available to tests
    shadow.extendsFrom compileOnly
    testRuntime.extendsFrom compileOnly
}

dependencies {
    compile project(':conductor-core')

    //ES5 Dependency
    compile "org.apache.logging.log4j:log4j-api:${revLog4jApi}"
    compile "org.apache.logging.log4j:log4j-core:${revLog4jCore}"

    compileOnly "commons-io:commons-io:${revCommonsIo}"

    compileOnly "org.elasticsearch:elasticsearch:${revElasticSearch2}"
    compileOnly "com.github.rholder:guava-retrying:${revGuavaRetrying}"
}

// Drop the classifier and delete jar task actions to replace the regular jar artifact with the shadow artifact
shadowJar {
    configurations = [project.configurations.shadow]
    classifier = null

    // Service files are not included by default.
    mergeServiceFiles {
        include 'META-INF/services/*'
        include 'META-INF/maven/*'
    }
}

jar.enabled = false
jar.dependsOn shadowJar

configureRelocationShadowJar {
    prefix = 'conductor'
}