plugins {
    id "com.github.johnrengelman.shadow" version "6.1.0"
    id 'java'
}

configurations {
    // Prevent shaded dependencies from being published, while keeping them available to tests
    shadow.extendsFrom compileOnly
    testRuntime.extendsFrom compileOnly
}

dependencies {
    compile project(':conductor-core')

    compile "commons-io:commons-io:${revCommonsIo}"

    compileOnly "org.elasticsearch:elasticsearch:${revElasticSearch6}"
    compileOnly "org.elasticsearch.client:transport:${revElasticSearch6}"
    compileOnly "org.elasticsearch.client:elasticsearch-rest-client:${revElasticSearch6}"
    compileOnly "org.elasticsearch.client:elasticsearch-rest-high-level-client:${revElasticSearch6}"

    compile "org.apache.logging.log4j:log4j-api:${revLog4jApi}"
    compile "org.apache.logging.log4j:log4j-core:${revLog4jCore}"

    // Test dependencies
    testCompile "org.slf4j:slf4j-log4j12:${revSlf4jlog4j}"
    testCompile "org.awaitility:awaitility:${revAwaitility}"

    testCompile "org.elasticsearch:elasticsearch:${revElasticSearch6}"
    testCompile "org.elasticsearch.client:transport:${revElasticSearch6}"
    testCompile "org.elasticsearch.client:elasticsearch-rest-client:${revElasticSearch6}"
    testCompile "org.elasticsearch.client:elasticsearch-rest-high-level-client:${revElasticSearch6}"
}

// Drop the classifier and delete jar task actions to replace the regular jar artifact with the shadow artifact
shadowJar {
    configurations = [project.configurations.shadow]
    classifier = null

    // Service files are not included by default.
    mergeServiceFiles {
        include 'META-INF/services/*'
        include 'META-INF/maven/*'
    }
}

jar.enabled = false
jar.dependsOn shadowJar

/**
 * Shadow is shipped with a task that can be used to automatically configure all packages from all dependencies to be relocated.
 * This feature was formally shipped into a 2nd plugin (com.github.johnrengelman.plugin-shadow) but has been removed for clarity reasons in version 4.0.0.
 */
// Configure Auto Relocation
import com.github.jengelman.gradle.plugins.shadow.tasks.ConfigureShadowRelocation

task relocateShadowJar(type: ConfigureShadowRelocation) {
    target = tasks.shadowJar
    prefix = "conductor"
}

tasks.shadowJar.dependsOn tasks.relocateShadowJar
