buildscript {
    dependencies {
        classpath "org.akhikhl.gretty:gretty:1.2.4"
    }
}

plugins {
    id 'com.github.johnrengelman.shadow' version '1.2.3'
}

configurations.all {
    resolutionStrategy {
        force 'com.fasterxml.jackson.core:jackson-core:2.7.5'
    }
}

apply plugin: 'war'
apply plugin: "org.akhikhl.gretty"

dependencies {
    // Log4j
    compile 'org.apache.logging.log4j:log4j-core:2.9.0'
    compile 'org.apache.logging.log4j:log4j-jul:2.9.0'
    compile 'org.slf4j:jul-to-slf4j:1.7.25'

    //Conductor
    compile project(':conductor-core')
    compile project(':conductor-jersey')
    compile project(':conductor-redis-persistence')
    compile project(':conductor-contribs')
    compile project(':conductor-es5-persistence')

    //Jetty
    compile 'org.eclipse.jetty:jetty-server:9.3.9.+'
    compile 'org.eclipse.jetty:jetty-servlet:9.3.9.+'

    //Guice
    compile 'com.sun.jersey.contribs:jersey-guice:1.19.+'
    compile 'com.google.inject:guice:4.+'
    compile 'com.google.inject.extensions:guice-servlet:4.1.+'

    //Swagger
    compile 'io.swagger:swagger-jersey-jaxrs:1.5.0'

    //In memory
    compile 'org.rarefiedredis.redis:redis-java:0.0.17'

    // JNA library for EmbeddedElasticSearch
    compile 'net.java.dev.jna:jna:4.1.0'
}

shadowJar {
    mergeServiceFiles()
    configurations = [project.configurations.compile]
    manifest {
        attributes 'Description': 'Self contained Conductor server jar'
        attributes 'Main-Class': 'com.netflix.conductor.server.Main'
    }
}
publishing {
    publications {
        nebula(MavenPublication) {
            artifact shadowJar
        }
    }
}

gretty {
    contextPath = '/'
    servletContainer = 'tomcat8'
    scanDirs = ['**/src/main/resources/**']
    scanDependencies = true
    port = 8080
    // More properties can be found here:
    // http://akhikhl.github.io/gretty-doc/Gretty-configuration.html
}
configurations.grettyRunnerTomcat8 {
    exclude group: 'org.slf4j', module: 'log4j-over-slf4j'
}


build.dependsOn('shadowJar')

task server(type: JavaExec) {
    //setDebug(true)

    environment 'TLD', 'owf-dev'
    environment 'STACK', 'test'

    // Dynomite settings
    environment 'db', 'elasticsearch'

    // Workflow settings
    environment 'workflow_failure_expandInline', 'false'
    environment 'decider_sweep_frequency_seconds', '5'

    // Elasticsearch settings
    environment 'workflow_elasticsearch_mode', 'elasticsearch'
    environment 'workflow_elasticsearch_service', 'workflows-search-tcp.service.owf-dev'
    environment 'workflow_elasticsearch_cluster_name', 'workflows.search'

    // NATS settings
    environment 'io_nats_client_url', 'nats://localhost:4222'
    environment 'conductor_additional_modules', 'com.netflix.conductor.contribs.NatsModule'

    // Auth settings
    environment 'conductor_auth_url', 'https://auth.dmlib.de/v1/tenant/deluxe/auth/token'
    environment 'conductor_auth_clientId', 'deluxe.conductor'
    environment 'conductor_auth_clientSecret', '4ecafd6a-a3ce-45dd-bf05-85f2941413d3'

    environment 'loadSample', 'false'
    main = 'com.netflix.conductor.server.Main'
    classpath = sourceSets.test.runtimeClasspath
}