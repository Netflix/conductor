buildscript {
	
	repositories {
        jcenter()
    }
    
    dependencies {
        classpath 'com.netflix.nebula:gradle-extra-configurations-plugin:3.0.3'
        classpath 'org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:2.8'
        classpath 'org.apache.ant:ant:1.9.7'
    }
}
plugins {
    id 'nebula.netflixoss' version '3.6.0'
}

// Establish version and status
ext.githubProjectName = rootProject.name // Change if github project name is not the same as the root project's name

apply plugin: 'project-report'
apply plugin: 'org.sonarqube'

subprojects {

    apply plugin: 'nebula.netflixoss'
    apply plugin: 'nebula.provided-base'
    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: 'eclipse'
    apply plugin: 'project-report'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    repositories {
        jcenter()
    }

    configurations.all {
        resolutionStrategy.eachDependency { details ->
            if (details.requested.group == 'org.apache.logging.log4j' && details.target.version < '2.15.0') {
                details.useVersion '2.15.0'
                details.because 'CVE-2021-44228'
            }
        }
    }

    dependencies {
    	testCompile 'junit:junit-dep:4.10'
    	testCompile 'org.mockito:mockito-all:1.10.0'
    }

    group = "com.netflix.${githubProjectName}"
    
    tasks.withType(Test)  {
        maxParallelForks = 100
    }
    
    license {
		excludes(['**/*.txt', '**/*.conf', '**/*.properties', '**/*.json', '**/swagger-ui/*'])
	}
	
	task licenseFormatTests (type:nl.javadude.gradle.plugins.license.License) {
    	source = fileTree(dir: "src/test").include("**/*")
	}
	licenseFormat.dependsOn licenseFormatTests

}
